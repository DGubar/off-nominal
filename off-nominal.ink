    
// =================================================================
// НЕШТАТНАЯ СИТУАЦИЯ: БАЗА ДАННЫХ ИГРЫ (ПЕРЕМЕННЫЕ)
// =================================================================

// -----------------------------------------------------------------
// 1. ОСНОВНЫЕ ПАРАМЕТРЫ КАПИТАНА
// -----------------------------------------------------------------
VAR ustav_score = 0
VAR precedent_score = 0

// -----------------------------------------------------------------
// 2. ОТНОШЕНИЯ С КОМАНДОЙ
// -----------------------------------------------------------------
VAR ignat_relationship = 0
VAR lana_relationship = 0

// -----------------------------------------------------------------
// 3. ГЛОБАЛЬНЫЕ ФЛАГИ (СОСТОЯНИЯ МИРА)
// -----------------------------------------------------------------
// Эпизод 1: Архив
VAR ep1_archive_state = "unknown" // -> "science_saved", "culture_saved"
// Эпизод 2: Источник
VAR ep2_source_state = "unknown" // -> "harvest_saved", "lifeform_saved"
// Эпизод 3: Перекресток
VAR ep3_crossroads_state = "unknown" // -> "order_restored", "justice_served"
// Эпизод 4: Колыбель
VAR ep4_cradle_state = "unknown" // -> "protocol_followed", "will_respected"
// Эпизод 5: Глубина
VAR ep5_depth_state = "unknown" // -> "majority_saved", "all_or_nothing"
// Эпизод 6: Щит
VAR ep6_shield_state = "unknown" // -> "damage_minimized", "station_restored"
// Эпизод 7: Финал
VAR ep7_finale_state = "pending"
 
// -----------------------------------------------------------------
// 4. СЛУЖЕБНЫЕ ПЕРЕМЕННЫЕ (могут понадобиться по ходу)
// -----------------------------------------------------------------
VAR current_episode = 1 // Для отслеживания прогресса
VAR game_over = false // Если в каком-то эпизоде возможен проигрыш
VAR ustav_perk_unlocked = false // Флаг системного бонуса Устава
VAR precedent_perk_unlocked = false // Флаг системного бонуса Прецедента

  

// =================================================================
// НАЧАЛО ИГРЫ
// =================================================================
-> intro
=== intro ===
#Location: Captains_Quarters
Бортовой журнал капитана Урсана Буреломова.
В Спасательной cлужбе есть поговорка: «В Уставе есть ответ на любой случай, но решение все равно принимать тебе». За годы полетов я убедился в ее правоте. Каждый рейс — это проверка твоего внутреннего компаса. Проверка того, где проходит грань между буквой протокола и его духом.

#Location: Captains_bridge
Этот месяц должен был стать последним перед Великой Спячкой — рутинные дежурства, бумажная работа. Но Командование решило иначе. Меня назначили на новый корабль — «Зимородок» — и дали двух блестящих, но совершенно «зеленых» выпускников Академии. Это не простое назначение для них и для меня, это — испытательный срок.
Игнат Гладков — инженерный гений, прагматик, для которого любая проблема — это система, требующая элегантного решения.
Лана Верескова — ксенобиолог, идеалистка, для которой любая жизнь, даже самая малая — бесценна.
Они — лучшие в своих областях. И они — мое новое испытание. Я еще не знаю, что этот месяц станет самым сложным в моей карьере. Что каждое принятое решение будет взвешиваться не только мной, но и Советом. И что по его итогам мне предстоит дать ответ на главный вопрос: что на самом деле значит быть спасателем?

+ [Начать рейс «Зимородка»]
    -> introduction

-> introduction

=== introduction ===
#Location: Captains_bridge
Бортовой журнал капитана Урсана Буреломова. Спасательный борт «Зимородок».
Мы покинули доки базы «Перевал Тысячи Ветров» по графику. Короткое и яркое лето в предгорьях почти закончилось, скоро Осенний совет и Великая Спячка. Тепеь нам предстоят рутинные облеты — последняя инспекция перед долгой зимой. Команда на борту: бортинженер Игнат Гладков, ксенобиолог Лана Верескова. 
Первая точка маршрута — биостанция «Летний Сад». Час назад оттуда поступил сигнал о биологической угрозе. Похоже, месяц не будет рутинным.
+ [Вылететь на биостанцию]
    -> episode_1


// =================================================================
// ЭПИЗОД 1: БИОСТАНЦИЯ "ЛЕТНИЙ САД"
// =================================================================

=== episode_1 ===
#Location: Podlet_k_Letnemu_Sadu
Мы на подлете к «Летнему Саду». Здание архива этой старейшей биостанции переведено на карантин. Поступает доклад от директора Майской: утечка экспериментального удобрения вызвала взрывной рост коррозионной плесени. Персонал в безопасности, но два ключевых хранилища загерметизированы и медленно пожираются грибком. У нас на борту есть ограниченный запас нейтрализующего реагента. Мы успеем очистить только одно крыло, прежде чем содержимое второго будет уничтожено.

*   [Запросить мнения команды] -> ep1_dilemma

= ep1_dilemma
#Location: Captains_bridge
Игнат: Капитан, данные телеметрии на экране. В крыле «Геном» — результаты исследований за последние двадцать лет. Весь наш авангард в селекции медоносов. Потеря этих данных отбросит продовольственную программу на десятилетия. Это системный приоритет. #Н

Лана: А в крыле «Корни» — вся история биостанции и поселка! Личные архивы, голограммы основателей, рецепт медовухи Прасковьи Потаповой, который передавался из поколения в поколение... Это же их душа, их память! Мы не можем позволить ей исчезнуть. #П

Игнат: Душа — это абстракция. А неурожаи и голод в отдаленных поселениях — вполне реальная перспектива, если мы потеряем эти наработки. Устав требует сохранять стратегические активы. Это — «Геном». #З

Лана: Устав написан для того, чтобы помогать медведям, а не цифрам в отчетах! Никто не останется голодным, что ты придумываешь. Ну да, урожаи будут поменьше. Что важнее: спасти то, что нельзя никак восстановить, или то, что восстанавливать просто долго?.. #З

*   [Принять решение] -> ep1_choice

= ep1_choice
#Location: Captains_bridge
Урсан Буреломов: Времени мало. Плесень действует быстро. Нужно выбрать, какое крыло спасать.

+ [Игнат прав. Глобальный рогресс цивилизации важнее мелких артефактов прошлого. Спасаем «Геном».]
    -> ep1_resolve_ustav

+ [Лана права. История и память делают нас теми, кто мы есть. Спасаем «Корни».]
    -> ep1_resolve_precedent

= ep1_resolve_ustav
// --- ПОСЛЕДСТВИЯ ВЫБОРА "УСТАВ" ---
~ ustav_score = ustav_score + 10
~ precedent_score = precedent_score - 5
~ ignat_relationship = ignat_relationship + 10
~ lana_relationship = lana_relationship - 5
~ ep1_archive_state = "science_saved"

#Location: Captains_bridge
Я отдаю приказ направить роботов с реагентом в крыло «Геном». Через час глава станции докладывает: распространение плесени остановлено, архив передовых научных данных спасен. Но из крыла «Корни» больше не поступает никаких сигналов. Все личные архивы, история станции и ее культурное наследие уничтожены.

Игнат: Это было правильное решение, капитан. Тяжелое, но правильное. Мы сохранили важнейшие наработки для будущего. #Р

Лана: Да уж, спасли для будущего... Зато прошлое все потеряли!.. #П

+ [К следующей цели]
    -> episode_2

= ep1_resolve_precedent
// --- ПОСЛЕДСТВИЯ ВЫБОРА "ПРЕЦЕДЕНТ" ---
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship + 10
~ ignat_relationship = ignat_relationship - 5
~ ep1_archive_state = "culture_saved"

#Location: Captains_bridge
Вы приказываете спасать крыло «Корни». Роботы успешно нейтрализуют плесень, сохраняя бесценные личные архивы и артефакты станции. Но из научного крыла приходит отчет: данные по селекции за последние двадцать лет очень сильно повреждены, навряд ли их восстановят. Придется нарабатывать с начала.

Лана: Спасибо, капитан. Вы сохранили то, что делает это место живым. Вы сохранили их память. #Р

Игнат: Мы сохранили память ценой будущего. Надеюсь, эта память поможет им, когда возникнут проблемы с продовольствием. Это безответственное решение. #З

+ [К следующей цели, получен новый сигнал бедствия!]
    -> episode_2

// =================================================================
// ЭПИЗОД 2: АГРАРНОЕ ПОСЕЛЕНИЕ "ТИХИЙ ИСТОЧНИК"
// =================================================================

=== episode_2 ===
#Location: Podlet_k_Tihomu_Istochniku
Мы на подлете к аграрному поселению «Тихий Источник». Сигнал бедствия подтвержден. Этот аванпост — одна из главных житниц сектора, он целиком зависит от теплиц, которые питаются от уникального геотермального источника. 
Староста Потапов: Мощность источника падает, капитан, и температура в теплицах снижается. Если ничего срочно не предпринять, большая часть урожая погибнет, больно он у нас теплолюбивый... А раз своего запаса не будет, придется завозить фрукты зимой тоннами, не можем же мы ребятишек без фруктов оставить?.. Вы уж помогите!

// --- НОВЫЙ БЛОК: ОЦЕНКА СИТУАЦИИ ---
#Location: Captains_bridge
Я бросаю взгляд на команду. После событий на «Летнем Саде» атмосфера на мостике изменилась.
{
    - ignat_relationship <= 0: Игнат подчеркнуто формален. Он выполняет приказы, но в каждом его движении сквозит холодное несогласие с моим предыдущим решением.
    - else: // Отношения > 0, но < 20
        Выбор на «Летнем Саде», похоже, укрепил доверие Игната ко мне. Он работает четко и сосредоточенно, время от времени встречаясь со мной взглядом, словно ища подтверждения, что мы на одной волне.
}
{
    - lana_relationship <= 0: Лана избегает моего взгляда. Она явно разочарована моим выбором и держит дистанцию.
    - else: // Отношения > 0, но < 20
        Решение по архивам явно нашло у Ланы отклик. Она работает с воодушевлением, и я чувствую ее молчаливую поддержку.
}

Староста Потапов: Мы пока тут изучили фильтры, и понимаем, что причина сбоя — разросшаяся колония редких, очень пугливых микроорганизмов, которые реагируют на работу оборудования и забивают каналы.

*   [Запросить мнения команды] -> ep2_dilemma

= ep2_dilemma
#Location: Captains_bridge
Игнат: Капитан, я проанализировал данные с зондов. Проблема ясна, и решение есть. Мы можем использовать низкочастотный звуковой импульс. Он гарантированно очистит каналы от биомассы за один час и восстановит полную мощность источника. Мы спасем урожай, целое поселение останется независимым от поставок, получит свою норму витаминов. #Н

Лана: Уничтожить? Игнат, это не просто «биомасса», это уникальная эндемичная экосистема! Возможно, единственный подобный вид во всей галактике. Мы не можем просто уничтожить! #У

Игнат: Лана, на кону жизни сотен медведей, которые останутся без достаточного запаса еды. Речь же не только фруктах, а об общей продовольственной безопасности. Устав однозначно ставит благополучие разумных существ выше неразумной флоры и фауны. Импульс — единственное верное решение. #З

Лана: Есть и другой путь! Мы можем запустить наших микро-роботов для бережного «переселения» колонии в специальный биоконтейнер. Да, это займет много времени, и часть урожая будет потеряна. Но мы спасем и поселение, и уникальный вид. Наше суждение должно учитывать интересы всей биосферы. #Н

*   [Принять решение] -> ep2_choice

= ep2_choice
#Location: Captains_bridge
Урсан Буреломов: Снова выбор между системной эффективностью и сохранением уникальной жизни. Решать мне.

+ [Игнат прав. Благополучие поселения — наш главный приоритет. Активировать импульс.]
    -> ep2_resolve_ustav

+ [Лана права. Мы не можем совершать экоцид. Начинаем операцию по переселению.]
    -> ep2_resolve_precedent


= ep2_resolve_ustav
// --- ПОСЛЕДСТВИЯ ВЫБОРА "УСТАВ" ---
~ ustav_score = ustav_score + 10
~ precedent_score = precedent_score - 5
~ ignat_relationship = ignat_relationship + 10
~ lana_relationship = lana_relationship - 5
~ ep2_source_state = "harvest_saved"

#Location: Captains_bridge
Короткий, низкий гул слышен даже на «Зимородке», когда импульс уходит в термальную трещину. Через несколько минут индикаторы мощности источника на главном экране уверенно ползут вверх. Глава поселения присылает рапорт с благодарностью. Урожай спасен.
Одновременно с этим био-сканеры показывают полное отсутствие жизни в геотермальных каналах.

Игнат: Чистая работа, капитан. Поселение будет с урожаем. Мы выполнили свою задачу согласно уставу. #Н

Лана: Мы только что стерли с карты целую ветвь эволюции. Надеюсь, их урожай стоил того... #П

+ [К следующей цели]
    -> interlude_1 // Переход к первой интерлюдии


= ep2_resolve_precedent
// --- ПОСЛЕДСТВИЯ ВЫБОРА "ПРЕЦЕДЕНТ" ---
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship + 10
~ ignat_relationship = ignat_relationship - 5
~ ep2_source_state = "lifeform_saved"

#Location: Captains_bridge
Операция занимает почти весь день. Микроскопические роботы-«пастухи» бережно собирают колонию. Наконец, Лана докладывает, что контейнер с биомассой благополучно доставлен на борт. 
Вскоре выходит на связь глава поселения. Он благодарит за спасение, но с горечью сообщает о потере почти трети урожая. Зима будет трудной.

Лана: Спасибо, капитан. Для Вселенной это, возможно, важнее любого урожая. Вы приняли правильное решение, биоразнообразие необходимо поддерживать! #Р

Игнат: Я занесу в отчет: «Миссия выполнена частично с превышением временных и ресурсных нормативов». Мы спасли микробов, но обрекли поселение на перебои с витаминами. Это было безответственно. #З

+ [К следующей цели]
    -> interlude_1 // Переход к первой интерлюдии

// =================================================================
// ИНТЕРЛЮДИЯ 1: БАЗА "ПЕРЕВАЛ ТЫСЯЧИ ВЕТРОВ"
// =================================================================

=== interlude_1 ===
#Location: Base_Lounge
«Зимородок» в доке на базе «Перевал Тысячи Ветров». Дозаправка, пополнение запасов и, конечно, отчеты, отчеты, отчеты. 
На терминале в моей каюте уже мигает значок входящего сообщения от Командования. Время узнать официальную оценку наших действий.

*   [Прочитать отчет] -> int1_report

= int1_report
#Location: Base_Terminal
ОФИЦИАЛЬНЫЙ ОТЧЕТ. РЕЙС 34-ГАММА. КАПИТАН УРСАН БУРЕЛОМОВ.

МИССИЯ 1: Биостанция «Летний Сад».
{ep1_archive_state == "science_saved":
    Решение о спасении научного крыла «Геном» полностью соответствует статье 12 Устава. Отмечается приверженность протоколу и системный подход.
- else: // culture_saved
    Решение о спасении общественного крыла «Корни» зафиксировано как отклонение от протокола с пометкой «принято под личную ответственность капитана».
}

МИССИЯ 2: Поселение «Тихий Источник».
{ep2_source_state == "harvest_saved":
    Быстрое и эффективное решение проблемы с геотермальным источником заслуживает похвалы. Благополучие поселения было обеспечено в кратчайшие сроки.
- else: // lifeform_saved
    Операция по переселению микроорганизмов привела к значительным потерям урожая и классифицирована как «частичный успех с превышением ресурсных нормативов».
}

Сухо. Безлично. Как всегда.
*   [Осмотреться в кают-компании] -> int1_lounge

= int1_lounge
#Location: Base_Lounge
Кают-компания базы гудит. Пилоты, инженеры, навигаторы — все обсуждают последние новости. Проходя мимо одного из столиков, я невольно улавливаю обрывки разговора...

    // --- БЛОК ОБСУЖДЕНИЯ ЭПИЗОДА 1 ("АРХИВ") ---
    { ep1_archive_state == "science_saved":
        Инженер Компас: ...и правильно сделал Буреломов! Эти архивы — ключ к терраформированию. Десятки будущих колоний скажут ему спасибо. Это стратегическое мышление! #Н
        Штурман Сокол: Стратегическое? Он стер историю целого поселения. Это как отрезать корни у дерева. Да, оно постоит еще, но уже мертвое. Бездушный подход!.. #П
    - else: // culture_saved
        Техник Искра: Говорят, Буреломов на «Летнем Саде» спас музей вместо лаборатории. Настоящий герой! Понимает, что мы не просто винтики в системе. #Р
        Снабженец Улей: Герой? Да он отбросил аграрную науку на двадцать лет назад из-за сентиментальности. Посмотрим, как он запоет, когда из-за его «геройства» где-нибудь начнется голод. Безответственность! #З
    }
    
    // --- БЛОК ОБСУЖДЕНИЯ ЭПИЗОДА 2 ("ИСТОЧНИК") ---
    { ep2_source_state == "harvest_saved":
        Инженер Компас: А на «Источнике» он молодец. Решил проблему быстро, урожай спасен. Так и должен работать спасатель: четко, по протоколу. #Н
        Штурман Сокол: «Четко, по протоколу»... да он просто стер из вселенной то, чего даже не попытался понять! Это не работа спасателя, а банальная зачистка. Узость мышления! Сегодня он так поступает с «биомассой», завтра с неудобными активными зверушками, а потом что, зачистит какого-нибудь упрямого «ненужного» старика? #З
    - else: // lifeform_saved
        Снабженец Улей: Я читал отчет. Он потратил целый день на спасение каких-то бактерий! Это же прецедент! Теперь каждый биолог будет требовать такого же отношения. Ресурсов не напасешься! #З
        Техник Искра: А я считаю, это единственно верный поступок. Он показал, что мы не варвары, уничтожающие все на своем пути. Это дальновидность. Кто знает, какие открытия скрывала эта форма жизни? #У
    }

Похоже, однозначного мнения нет ни у кого.
*   [Вернуться к себе] -> int1_dialogue

= int1_dialogue
#Location: Base_Lounge
В коридоре за моей спиной слышны шаги.

{
    - ustav_score > precedent_score:
        // Если игрок больше склонялся к Уставу, к нему подходит Лана
        -> int1_lana_talk
    - precedent_score > ustav_score:
        // Если игрок больше склонялся к Прецеденту, к нему подходит Игнат
        -> int1_ignat_talk
    - else:
        // Если счет равный, подходят оба
        -> int1_neutral_talk
}
= int1_lana_talk
Лана: Капитан, можно вас на пару слов? #Н
Лана: Я видела отчеты. И слышала разговоры. Вы действуете строго по уставу. Это надежно, это безопасно. Но каждый раз, когда мы выбираем универсальное правило, мы игнорируем уникальные нюансы ситуации. #П
Лана: Создание прецедента — это всегда ресурсозатратно, я понимаю. Требует времени, сил, личного участия. Но только так можно найти по-настоящему справедливое решение, а не просто «правильное» по инструкции. Я прошу вас, капитан, не бойтесь вникать в детали. #Н
+ [Я вас услышал, Лана.]
    -> int1_end

= int1_ignat_talk
Игнат: Капитан. Минуту. #Н
Игнат: Я вижу, вы предпочитаете индивидуальный подход в каждом случае. Это вызывает уважение. Но вы создаете опасные прецеденты. Сегодня мы тратим день на спасение микробов, завтра — неделю на уговоры одного упрямца. #П
Игнат: Устав — это не просто инструкция. Это система, выверенная сотнями таких случаев, как наши. Она усредняет, да. Но она же и защищает от ошибок, вызванных личным суждением. Хаос начинается там, где каждый начинает трактовать правила по-своему. Я надеюсь, в будущем мы будем больше доверять системе. #Н
+ [Я принял ваши слова к сведению, Игнат.]
    -> int1_end

= int1_neutral_talk
#Location: Base_Captains_Quarters
Меня почти одновременно догоняют и Игнат, и Лана. Они останавливаются, смотрят друг на друга, потом на меня.

Игнат: Капитан. Мы с Ланой... обсуждали ваши решения. #Н
Лана: Вы пытаетесь найти баланс. Это видно. Но, капитан, в наших ситуациях не бывает «золотой середины». #П
Игнат: Она права. Выбирая то устав, то прецедент, вы не создаете новую философию. Вы просто не выбираете ни одну. Непоследовательность — худшая стратегия в кризис. Команда должна понимать, как вы мыслите. И, честно говоря, мы пока не понимаем. #З
Лана: Если жы вы так стремитесь к балансу... Капитан, единицы способны балансировать там, где необходима четкость! Но если уж так, то вам потребуется поддержка нас обоих. #Н
+ [Я обдумаю ваши слова, команда.]
    -> int1_end

= int1_end
#Location: Captains_bridge
Разговор окончен, но вопросы остались. Впереди — новые вызовы. Нужно готовиться.
+ [Проложить курс]
    -> episode_3

// =================================================================
// ЭПИЗОД 3: СТАНЦИЯ "ПЕРЕКРЕСТОК"
// =================================================================

=== episode_3 ===
#Location: Orbit_Crossroads
Подлетаем к орбитальной транзитной станции «Перекресток». В эфире хаос. Глава станции Боровик докладывает: техногенная авария. Из-за сбоя системы распределения энергомеда два грузовых корабля намертво состыкованы с аварийным шлюзом. На одном корабле — местные старожилы, а на втором — молодой кооператив перевозчиков. Это буквально их третий рейс на этом корабле. 
// --- БЛОК ОЦЕНКИ СИТУАЦИИ ---
#Location: Captains_bridge
Ну вот и снова нештатная ситуация. Я смотрю, как команда реагирует на новый вызов.
{
    - ignat_relationship >= 20: Игнат работает как идеально отлаженный механизм. Он полностью доверяет моим приказам, предвосхищая их. Наше взаимодействие почти не требует слов.
    - ignat_relationship <= 0: Напряжение между нами с Игнатом ощущается физически. Он избегает прямого контакта, его ответы на мои вопросы — предельно краткие и формальные.
    - else: Игнат сохраняет профессиональную дистанцию, но я чувствую, что он внимательно следит за каждым моим решением, все еще оценивая меня.
}
{
    - lana_relationship >= 20: Лана стала настоящим моральным компасом корабля. Ее поддержка ощущается во всем — от ободряющей улыбки до вовремя поданной кружки горячего напитка.
    - lana_relationship <= 0: Лана замкнулась в себе. Она выполняет свою работу, но от былого энтузиазма не осталось и следа. Мостик для нее стал просто рабочим местом.
    - else: Лана сосредоточена и серьезна. Она ждет, какой путь я выберу на этот раз, и ее уважение еще предстоит заслужить.
}

Экипажи в своих кораблях, и если не разобраться с шлюзом, им грозит переохлаждение. Местные обвиняют в аварии новичков, те — местных. Никто не хочет сотрудничать.

*   [Запросить мнения команды] -> ep3_dilemma

= ep3_dilemma
#Location: Captains_bridge
Игнат: Капитан, анализ ситуации готов. Самый быстрый способ — применить плазменные резаки и отсечь аварийный шлюз от обоих кораблей. Операция займет два часа. Оба экипажа будут спасены. Это соответствует уставу: минимизация угрозы жизни в кратчайшие сроки. #Н

Лана: «Отсечь»? Игнат, это уничтожит все оборудование на стыковочном узле старожилов и может повредить корабль кооператива! Мы спасем экипажи сейчас, но оставим их без кораблей на долгие месяцы. Это не помощь. #З

Игнат: Наша задача — спасти жизни, а не оборудование. Устав не требует от нас заниматься ремонтом чужих кораблей, тем более когда ситуация критическая. Резаки — это чистое, системное решение. #Н

Лана: Есть другой путь! Мы можем помочь им перезапустить систему шлюзов вручную. Это сохранит оба корабля. Да, для этого нам придется заставить их инженеров работать вместе. Потратить время на переговоры, выступить посредником. Но только так мы решим проблему по-настояшему, а не просто ампутируем больное. Это потребует от нас личного участия и внимания. #Н

*   [Принять решение] -> ep3_choice

= ep3_choice
#Location: Captains_bridge
Урсан Буреломов: Снова выбор. Быстрое спасение по уставу ценой материальных потерь. Или долгое, комплексное решение, требующее дипломатии.

+ [Игнат прав. Жизни экипажей — абсолютный приоритет. Готовьте резаки.]
    -> ep3_resolve_ustav

+ [Лана права. Мы должны попытаться спасти не только медведей, но и их будущее. Начинаем переговоры.]
    -> ep3_resolve_precedent


= ep3_resolve_ustav
// --- ПОСЛЕДСТВИЯ ВЫБОРА "УСТАВ" ---
~ ustav_score = ustav_score + 10
~ precedent_score = precedent_score - 5
~ ignat_relationship = ignat_relationship + 10
~ lana_relationship = lana_relationship - 5
~ ep3_crossroads_state = "order_restored"

#Location: Captains_bridge
Операция проходит быстро и эффективно. Сноп плазмы отрезает искореженный шлюз. Оба корабля свободны. Их экипажи спасены и перемещены в теплые отсеки станции. 
Однако корабль кооператива получил критические повреждения и больше не сможет летать без капитального ремонта. Конфликт между фракциями только усилился.

Игнат: Все живы, капитан. Задача выполнена строго по уставу. Мы предотвратили трагедию. #Н

Лана: Мы спасли их сегодня, чтобы оставить без будущего завтра. Иногда следовать уставу — значит, выбирать самый простой, но не самый лучший путь. #П

+ [К следующей цели]
    -> episode_4


= ep3_resolve_precedent
// --- ПОСЛЕДСТВИЯ ВЫБОРА "ПРЕЦЕДЕНТ" ---
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship + 10
~ ignat_relationship = ignat_relationship - 5
~ ep3_crossroads_state = "justice_served"

#Location: Captains_bridge
Мы тратим почти целый день на челночную дипломатию. Выступая гарантом безопасности, я убедил инженеров с обоих кораблей работать вместе под руководством Игната. 
Это мучительно долго. Они спорят, обвиняют друг друга. Но в итоге, шаг за шагом, им удается перезапустить аварийную систему. Оба корабля отстыковываются своим ходом, почти без повреждений. Вражда сменилась усталым уважением.

Лана: Это было невероятно. Вы не просто спасли их, вы заставили их снова увидеть друг в друге честных медведей, а не врагов. Это и есть настоящая работа спасателя. #Р

Игнат: Мы потратили день на то, что можно было сделать за два часа. Целый сектор ждал, пока мы играли в психологов. Это было опасно и неэффективно. Мы отклонились от протокола и нам просто повезло. #З

+ [К следующей цели]
    -> episode_4
    
// =================================================================
// ЭПИЗОД 4: ПРОЕКТ "КОЛЫБЕЛЬ"
// =================================================================

=== episode_4 ===
#Location: Orbit_Cradle
Наш следующий пункт назначения — орбитальная станция «Колыбель». Отсюда пришел странный, прерывистый сигнал о каскадном сбое систем.
// --- БЛОК ОЦЕНКИ СИТУАЦИИ ---
#Location: Captains_bridge
Половина нашего испытательного срока позади, и команда уже не та, что была в начале.
{
    - ignat_relationship >= 20: // Доверие
        Я могу положиться на Игната, как на самого себя. Он не просто выполняет приказы — он понимает их логику. На мостике он мой надежный тыл.
    - ignat_relationship < 0: // Недоброжелательность (строго меньше нуля)
        Между мной и Игнатом выросла настоящая пропасть. Он открыто ставит под сомнение мои команды, апеллируя к Уставу, словно я курсант, а не капитан.
    - else: // Профессионализм [0, 19]
        { ep3_crossroads_state == "order_restored": // Если в прошлом эпизоде поддержали Игната
            После моей поддержки на «Перекрестке» лед в отношениях с Игнатом тронулся. Он все еще держит дистанцию, но уже не смотрит на меня с осуждением. Похоже, я вернул себе толику его профессионального уважения.
        - else: // Стабильная нейтральность
            Наши с Игнатом отношения остаются сугубо рабочими. Он — эффективный инженер, я — его командир. Он ждет моего следующего шага, чтобы составить окончательное мнение.
        }
}
{
    - lana_relationship >= 20: // Доверие
        Лана верит в меня. Она стала моим главным советником в вопросах, где протокол молчит. Ее присутствие делает самые тяжелые решения чуточку легче.
    - lana_relationship < 0: // Недоброжелательность (строго меньше нуля)
        Лана почти не разговаривает со мной вне рабочих вопросов. В ее глазах я вижу горькое разочарование, и это давит.
    - else: // Профессионализм [0, 19]
        { ep3_crossroads_state == "justice_served": // Если в прошлом эпизоде поддержали Лану
            Мое решение на «Перекрестке» явно воодушевило Лану. Она все еще осторожна, но готова к диалогу. Кажется, она видит во мне капитана, способного слышать не только букву Устава.
        - else: // Стабильная нейтральность
            Лана вежлива, но внимательно наблюдает за мной. Она все еще пытается понять, кто я такой, и, кажется, надеется, что я сделаю правильный выбор.
        }
}

Прибыв на место, мы видим последствия катастрофы. Энергосистема станции почти мертва. Сканеры показывают, что вся доступная энергия вручную перенаправлена на поддержание инкубатора с эмбрионами. Научное крыло, где находятся трое ученых, полностью обесточено. Они уже сделали свой выбор.
Наш корабль может дать достаточно энергии, чтобы отменить их решение и перезапустить систему жизнеобеспечения в научном крыле, но тогда энергии инкубатора хватит лишь на несколько минут.
Внезапно на связь выходит глава станции.

*   [Принять вызов] -> ep4_dilemma

= ep4_dilemma
#Location: Captains_bridge
Профессор Столетов: (изображение идёт помехами) Капитан Буреломов... слышу вас. Не вмешивайтесь. Мы... перенастроили систему. Это было наше решение. Наша работа должна жить... Спасайте инкубатор. Не отменяйте наш выбор... (и связь обрывается). #Н

Лана: Капитан, это агония, а не решение! Они в гипоксии, они не отвечают за свои слова! Протокол 7.4.1 обязывает нас спасать жизни, а не выполнять предсмертные просьбы. Мы должны немедленно перезапустить их систему жизнеобеспечения. Это наш прямой долг. #З

Игнат: Лана, ты не слышишь? Они не просят. Они сообщают о сделанном выборе. Они — лучшие инженеры-биохимики, они все рассчитали. Они уже пожертвовали собой! Отменить их действие — значит обесценить их подвиг, превратить их осмысленную жертву в бессмысленную трагедию. #П

Лана: Нет ничего бессмысленнее, чем позволить умереть тем, кого можно спасти! Мы спасатели, а не философы! Устав защищает жизнь, а не «смысл жертвы»! #З

Игнат: Иногда, чтобы защитить смысл жизни, нужно уважать право на самопожертвование. Мы должны создать прецедент, где мы уважаем волю великих медведей до самого конца. Это акт высшего признания их заслуг. #Н

*   [Принять решение] -> ep4_choice

= ep4_choice
#Location: Captains_bridge
Урсан Буреломов: Они уже сделали свой выбор. Вопрос в том, имею ли я право его отменить.

+ [Лана права. Мой долг — спасать жизни, а не уважать жертвы. Отменяю их решение, спасаем ученых.]
    -> ep4_resolve_ustav

+ [Игнат прав. Их жертва уже принесена. Я должен уважать их волю. Поддерживаем энергию инкубатора.]
    -> ep4_resolve_precedent


= ep4_resolve_ustav
// --- ПОСЛЕДСТВИЯ ВЫБОРА "УСТАВ" ---
~ ustav_score = ustav_score + 10
~ precedent_score = precedent_score - 5
~ ignat_relationship = ignat_relationship - 5
~ lana_relationship = lana_relationship + 10
~ ep4_cradle_state = "protocol_followed"

#Location: Captains_bridge
Я отдаю приказ перенаправить всю энергию на научное крыло. Системы жизнеобеспечения медленно перезапускаются. Через несколько часов Лана и Игнат докладывают, что все трое ученых спасены и находятся в медотсеке в тяжелом, но стабильном состоянии.
В тот же момент гаснут все индикаторы на схеме инкубатора. Генофонд десятков видов потерян навсегда.

Лана: Это было ужасно. Но вы выполнили свой долг, капитан. Вы спасли их. #П

Игнат: Мы отняли у них право на великий поступок. Превратили их подвиг в ничто. Мы спасли их тела, но растоптали их волю. Я не считаю это победой. #З

+ [К следующей цели]
    -> interlude_2


= ep4_resolve_precedent
// --- ПОСЛЕДСТВИЯ ВЫБОРА "ПРЕЦЕДЕНТ" ---
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship - 5
~ ignat_relationship = ignat_relationship + 10
~ ep4_cradle_state = "will_respected"

#Location: Captains_bridge
С тяжелым сердцем я принял их решение. Я приказываю направить энергию «Зимородка» на поддержку и стабилизацию систем инкубатора. Будущее десятков видов спасено.
Из научного крыла больше не поступает никаких сигналов. Три великих ученых ушли из жизни, но их последняя воля была исполнена.

Игнат: Они бы гордились вами, капитан. Вы отнеслись к ним с уважением, как к равным. Вы почтили их жертву. #Н

Лана: Мы стояли рядом и позволили им умереть. Мы могли их спасти, но не стали. Никакая высокая цель не может этого оправдать. Мы нарушили главную заповедь спасателя. #П

+ [К следующей цели]
    -> interlude_2

// =================================================================
// ИНТЕРЛЮДИЯ 2: ПОЛЕВОЙ ЛАГЕРЬ "ЗАТИШЬЕ"
// =================================================================

=== interlude_2 ===
#Location: Camp_fire
Вынужденная посадка для мелкого ремонта двигателя в глухой горной долине, не отмеченной на картах. Холодный свет лун едва пробивается сквозь низкие облака. 
После событий на «Колыбели» атмосфера в команде стала напряженной. Разговоры свелись к минимуму. Каждый думает о своем. Я сижу у небольшого костра и просматриваю накопившиеся сообщения.

*   [Проверить входящие] -> int2_messages

= int2_messages
#Location: Base_Terminal
На терминале несколько новых файлов. Официальный отчет Командования, несколько личных сообщений и... что-то из внутренней сети спасателей.

*   [Начать с официального отчета] -> int2_report

= int2_report
#Location: Base_Terminal
ОФИЦИАЛЬНЫЙ ОТЧЕТ. РЕЙС 34-ГАММА. КАПИТАН УРСАН БУРЕЛОМОВ.

МИССИЯ 3: Станция «Перекресток».
{ep3_crossroads_state == "order_restored":
    Быстрое разрешение кризисной ситуации признано образцовым. Действия по уставу позволили избежать эскалации и восстановить работу транспортного узла в кратчайшие сроки.
- else: // justice_served
    Отмечается превышение временных нормативов на 22 часа. Хотя конфликт и был разрешен без материальных потерь, создание прецедента с участием спасателей в роли медиаторов будет рассмотрено на Осеннем совете.
}

МИССИЯ 4: Станция «Колыбель».
{ep4_cradle_state == "protocol_followed":
    Действия капитана Буреломова по спасению персонала станции признаны полностью соответствующими Протоколу 7.4.1. Командование выражает благодарность за сохранение жизней ценных специалистов.
- else: // will_respected
    Действия капитана Буреломова, приведшие к гибели персонала, зафиксированы как грубое нарушение Протокола 7.4.1. Назначено внутреннее расследование. Полномочия капитана могут быть пересмотрены по возвращении.
}

*   [Проверить остальные сообщения] -> int2_bonus_check

= int2_bonus_check
#Location: Base_Terminal
{ ustav_score > precedent_score:
    // --- ВЕТКА ВЫСОКОГО УСТАВА ---
    ~ ustav_perk_unlocked = true
    // Формальное поощрение
    Входящее сообщение от пресс-службы флота. Тема: "Номинация".
    «Капитан Буреломов, ваши решительные и эффективные действия на станциях „Перекресток“ и „Летний Сад“ были отмечены Командованием. Вы номинированы на звание „Спасатель месяца“. Так держать!»
    
    // Личное сообщение
    Сообщение от Игната. «Капитан, я наблюдал за вашим подходом с самого начала своей стажировки, и сделал важный вывод. Вы последовательно доказываете, что системная эффективность — работающий принцип, который приносит результат, а не просто строка в Уставе. Вы вдохновили меня поискать точки оптимизации и в нашей работе. Я разработал скрипт для предполетной проверки, сокращающий ее время на 7%. Рапорт уже подан в Бюро оптимизации, возможно, его оценят и другие команды».
    
    + + [Посмотреть внутреннюю сеть] -> int2_lounge_talk

- else: // precedent_score >= ustav_score
    // --- ВЕТКА ВЫСОКОГО ПРЕЦЕДЕНТА ---
    ~ precedent_perk_unlocked = true
    // Неформальное признание
    Входящее сообщение. Тема: "Спасибо от всех нас". Открываю. Голограмма группы молодых перевозчиков в обнимку со старожилами со станции «Перекресток». «Капитан! Мы слышали, что начальство тобой недовольно. Плюнь. Ты единственный, кто отнесся к нам как к медведям, а не как к проблеме. Если будешь в нашем секторе — знай, для тебя и твоей команды всегда найдется кружка лучшей медовухи».
    
    // Бонус от мира
    Сообщение от Ланы. «Капитан, потрясающие новости со станции „Перекресток“! Вы поддержали решение разобраться, и это положило конец намечающейся вражде. Теперь обе группы основали общий ремонтный док! И на форуме пишут, что любой корабль Спасательной службы получает на „Перекрестке“ абсолютный приоритет в обслуживании. Ура, ура! Думаю, все спасатели вам сегодня салютуют».
    
    + + [Посмотреть внутреннюю сеть] -> int2_lounge_talk
}

= int2_lounge_talk
#Location: Base_Terminal
Открываю неформальный форум спасателей. В одной из веток обсуждают меня.

{ ustav_score > precedent_score:
    Бортники_рулят: Буреломов — машина. Четкий, эффективный, холодный как лед Нум-Торума. Работу делает, но я бы с ним в разведку не пошел.
    Чемпион_Арены: А я бы пошел. С ним по крайней мере знаешь, чего ожидать. Никаких сюрпризов, все по уставу. Надежно.
- else: // precedent_score >= ustav_score
    Бортники_рулят: Этот Буреломов — новая звезда. Настоящий Первопроходец, а не кабинетная крыса. Не боится брать на себя ответственность.
    Чемпион_Арены: «Не боится»? Он непредсказуемый! Сегодня он спас котенка, а завтра подставит под удар всю команду ради «справедливости». С такими опасно.
}

*   [Закрыть терминал] -> int2_dialogue

= int2_dialogue
#Location: Camp_fire
Я убираю терминал. Тишину нарушает чей-то голос.

{
    - ignat_relationship >= 20:
        // Если у игрока высокое доверие с Игнатом, он подходит с особым разговором
        -> int2_ignat_trust
    - lana_relationship >= 20:
        // Если высокое доверие с Ланой
        -> int2_lana_trust
    - else:
        // Стандартный, более нейтральный диалог
        -> int2_neutral_talk
}

= int2_ignat_trust
Игнат подходит и садится рядом, протягивая мне кружку горячего напитка.
Игнат: Тяжелый был день, капитан. Я много думал о ваших решениях на «Перекрестке» и «Колыбели». Вы доказали, что готовы брать на себя ответственность, а не просто прятаться за инструкцией. Это редкость. #Н
//дописать ответ Урсана
Игнат: Знаете, это навело меня на мысль... Устав хорош, но иногда он предлагает лишь достаточно хорошее решение, а не идеальное. Вот, есть стандартный протокол для новых реакторов модели 7-бис и выше. Он предписывает аварийное охлаждение, но это потому что инструкции еще не переписали. Я-то знаю, что есть способ их полной перезагрузки, и это более рационально и эффективно. Это не «против правил», это... ну как бы «над правилами»! Если нам когда-нибудь попадется такой реактор, и вы будете готовы к нештатному, но системно-верному решению... я готов предоставить расчеты. #Р
+ [Спасибо, Игнат. Я подумаю над этим.]
    -> int2_end

= int2_lana_trust
Лана подходит к костру и тихо садится напротив.
Лана: Капитан... Спасибо. #Н
Лана: Спасибо, что в самых страшных ситуациях вы не забыли о главном. О конкретной жизни, о памяти, о том, что нельзя измерять душами. #Р
Лана: Кстати, о жизнях. Помните полимер с «Источника»? Я закончила его анализ. Он действительно может служить временным био-щитом от радиации. Если в будущем нам придется столкнуться с утечкой, и кому-то понадобится пойти в опасную зону — это может спасти ему здоровье или даже жизнь. Это не по уставу, потому что требует нестандартных биостабилизаторов, но это шанс. Я синтезировала его и нанесла на один из запасных рабочих костюмов. Просто чтобы... ну, была возможность. #Р
+ [Спасибо, Лана. Это может пригодиться.]
    -> int2_end

= int2_neutral_talk
Я поднимаю голову. Игнат и Лана только что спорили, но вот стоят поодаль, каждый в своих мыслях. Никто не решается подойти. Мои решения создали трещину в команде. Или, может, она была там всегда, а я лишь сделал ее видимой. Но нужно двигаться дальше.
+ [Пора отправляться.]
    -> int2_end

= int2_end
#Location: Captains_bridge
Ремонт окончен. Мы снова в пути. Что же нас ждет?.. Кажется, получен новый сигнал!
+ [Проложить курс]
    -> episode_5 // Переход к следующему эпизоду

// =================================================================
// ЭПИЗОД 5: НАУЧНАЯ БАЗА "ГЛУБИНА"
// =================================================================

=== episode_5 ===
#Location: Spusk_k_Glubine
Мы на подлете к подледной базе «Глубина». Сигнал бедствия почти неразборчив, но слова «разгерметизация» и «взрыв реактора» звучат слишком отчетливо.

// --- БЛОК ОЦЕНКИ СИТУАЦИИ ---
События на «Колыбели» оставили глубокий след. Инверсия ролей, когда Лана отстаивала уставное решение, а Игнат говорил о необходимости принять сложный выбор ученых, заставила всех задуматься.
{
    - ignat_relationship >= 20: // Доверие
        Игнат, похоже, окончательно принял мой стиль командования. Он может быть не согласен в деталях, но он доверяет моему финальному решению, каким бы оно ни было.
    - ignat_relationship < 0: // Недоброжелательность
        Игнат перешел в режим «итальянской забастовки». Он делает только то, что прописано в инструкции, ни шагу в сторону. Любая моя инициатива встречается с его стороны глухой стеной.
    - else: // Профессионализм [0, 19]
        { ep4_cradle_state == "will_respected": // Если в прошлом эпизоде поддержали Игната
            Мое решение на «Колыбели» явно заставило Игната пересмотреть свое отношение ко мне. Холод в его взгляде сменился задумчивым интересом. Он видит, что я способен на неординарные, но сильные поступки.
        - else: // Стабильная нейтральность
            Наши с Игнатом отношения — это отношения командира и подчиненного. Он ждет, подтвержу ли я свой курс или снова удивлю его.
        }
}
{
    - lana_relationship >= 20: // Доверие
        Наши с Ланой взгляды на мир во многом совпадают, и это создает на мостике атмосферу единства. Я знаю, что в ее лице у меня всегда есть поддержка.
    - lana_relationship < 0: // Недоброжелательность
        Лана выглядит уставшей и подавленной. Она потеряла веру в то, что я способен сделать правильный, человечный выбор.
    - else: // Профессионализм [0, 19]
        { ep4_cradle_state == "protocol_followed": // Если в прошлом эпизоде поддержали Лану
            После того как я поддержал ее на «Колыбели», Лана снова видит во мне союзника. Профессиональная отстраненность сменилась теплотой. Она вновь готова доверять моему суждению.
        - else: // Стабильная нейтральность
            Лана погружена в работу. Кажется, она решила сосредоточиться на науке, чтобы меньше думать о моральных дилеммах. Она ждет от меня четкой позиции.
        }
}

Экстренная сводка от автоматики станции: в одном из лабораторных крыльев произошел прорыв ледяной коры. Отсек стремительно затопляется ледяной водой. Внутри заблокированы четверо ученых.
Одновременно с этим повреждение вызвало цепную реакцию в системе охлаждения главного энергохранилиза. Оно перегревается. Если не вмешаться, через час оно взорвется, уничтожив всю базу и всех, кто на ней — еще около тридцати медведей.

*   [Запросить мнения команды] -> ep5_dilemma

= ep5_dilemma
#Location: Captains_bridge
Игнат: Капитан, данные на экране. У нас нет времени на два действия. Мы должны срочно выбирать. #Н
Игнат: Вариант один: мы немедленно, дистанционно герметизируем аварийный отсек. Это остановит затопление и предотвратит дальнейшие сбои в системе. Мы пожертвуем четырьмя жизнями, но спасем реактор и остальные тридцать. Это жестоко, но это единственное математически верное решение. #Н

Лана: Нет! Мы не можем просто списать их со счетов! Мы спасатели! #З
Лана: Вариант два: мы отправляем штурмового робота, чтобы пробить завал и попытаться эвакуировать ученых из затопляемого отсека. Это займет почти все наше время. Шанс, что мы успеем после этого еще и стабилизировать реактор, — минимален. Мы рискуем потерять всех. Но мы должны попытаться! #П

Игнат: Лана, это же авантюра! Устав требует минимизировать потери. Пожертвовать тридцатью ради шанса спасти четверых — это не героизм, а преступная халатность! #З

Лана: А с каких пор мы начали измерять жизни в цифрах? Для тех четверых нет никакого «большинства»! Есть только их жизни, которые мы обязаны попытаться спасти! #З

*   [Принять решение] -> ep5_choice

= ep5_choice
#Location: Captains_bridge
Урсан Буреломов: Выбор между гарантированным спасением большинства и рискованной попыткой спасти всех.

+ [Игнат прав. Это ужасно, но мы должны спасти большинство. Герметизируем отсек.]
    -> ep5_resolve_ustav

+ [Лана права. Мы не можем бросать людей. Мы должны попытаться спасти всех, чего бы это ни стоило.]
    -> ep5_resolve_precedent


= ep5_resolve_ustav
// --- ПОСЛЕДСТВИЯ ВЫБОРА "УСТАВ" ---
~ ustav_score = ustav_score + 10
~ precedent_score = precedent_score - 5
~ ignat_relationship = ignat_relationship + 10
~ lana_relationship = lana_relationship - 5
~ ep5_depth_state = "majority_saved"

#Location: Captains_bridge
С тяжелым сердцем я отдаю приказ о герметизации. Автоматика с лязгом закрывает титановые переборки, отсекая затопленный отсек. Через несколько мучительных минут нам удается стабилизировать энергохранилище. Мы едва-едва успели. 
База спасена. Тридцать жизней — тоже. Четыре — потеряны навсегда.

Игнат: Вы приняли самое тяжелое, но единственно верное решение, капитан. Вы спасли станцию. #Н

Лана: Мы убили их. Мы нажали на кнопку и убили их. Никакой устав этого не оправдает... #П

+ [К следующей цели]
    -> episode_6 // Переход к следующему эпизоду


= ep5_resolve_precedent
// --- ПОСЛЕДСТВИЯ ВЫБОРА "ПРЕЦЕДЕНТ" ---
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship + 10
~ ignat_relationship = ignat_relationship - 5
~ ep5_depth_state = "all_or_nothing"

#Location: Captains_bridge
Я отдаю приказ рискнуть. Штурмовой робот пробивает завал. Начинается отчаянная гонка со временем. Я вижу на камерах, как спасатели вытаскивают ученых из ледяной воды полузатопленного отсека...
Внезапно весь экран заливает белым светом. Сигнал со станции пропадает. Датчики фиксируют мощнейший выброс энергии. Энергохранилище не выдержало.
Станция «Глубина» потеряна. Все тридцать четыре медведя погибли.

Лана: Я... я думала, мы успеем... Я была неправа... #П

Игнат: Ваша «надежда» только что убила их всех, капитан. Всех. Устав был написан именно для того, чтобы предотвращать вот такие катастрофы. #З

+ [К следующей цели]
    -> episode_6 // Переход к следующему эпизоду

// =================================================================
// Эпизод 6: ЩИТ
// =================================================================
=== episode_6 ===
#Location: Captains_Quarters
// --- БЛОК ОЦЕНКИ СИТУАЦИИ ---
После катастрофы на «Глубине» на корабле царит тяжелое напряжение.
{
    - ignat_relationship >= 20: // Доверие
        Несмотря на все споры и тяжелые решения, я знаю: Игнат доверяет мне. Я вижу это в том, как он без лишних вопросов выполняет подготовку к очередной нештатной ситуации. Он верит в своего капитана.
    - ignat_relationship < 0: // Недоброжелательность
        Отношения с Игнатом разрушены. Он общается со мной исключительно через бортовой журнал, оставляя рапорты. Он больше не считает меня лидером, которому можно доверять.
    - else: // Профессионализм [0, 19]
        { ep5_depth_state == "majority_saved": // Если в прошлом эпизоде поддержали Игната
            Мой тяжелый выбор на «Глубине» вернул мне уважение Игната. Он видит, что я способен принимать жесткие, но системно верные решения. В его взгляде — мрачное понимание и готовность работать слаженно.
        - else: // Стабильная нейтральность
            Мы с Игнатом — два профессионала в одной кабине, делающие свою работу перед лицом новой катастрофы. Личные разногласия отошли на второй план, но я знаю — он будет судить меня по каждому моему решению.
        }
}
{
    - lana_relationship >= 20: // Доверие
        Лана стала моей совестью. Даже когда я принимаю жесткие решения, я знаю, что она понимает их мотивы. Ее поддержка — мой главный ресурс.
    - lana_relationship < 0: // Недоброжелательность
        Лана сломлена. Она делает все на автомате, ее глаза пусты. Она видит во мне лишь бездушную машину или опасного авантюриста.
    - else: // Профессионализм [0, 19]
        { ep5_depth_state == "all_or_nothing": // Если в прошлом эпизоде поддержали Лану
            Мой фатальный приказ на «Глубине» показал Лане, что я готов бороться за каждую жизнь. Она подавлена, но я чувствую ее сочувствие, а не осуждение. Мы скорбим вместе.
        - else: // Стабильная нейтральность
            Лана держится отстраненно, но я вижу, что она смотрит на меня с надеждой. Она ждет, что я докажу, что человечность для меня — не пустое слово.
        }
}
Бортовой журнал капитана Урсана Буреломова. Запись после инцидента на базе «Глубина».
{ ep5_depth_state == "majority_saved":
    // --- ВЕТКА СПАСЕНИЯ БОЛЬШИНСТВА (УСТАВ) ---
    Станция спасена. Ситуация стабилизирована. Тридцать жизней вне опасности. Я отправил в Командование полный отчет. В нем всего одна строчка, которая имеет значение: операция проведена ценой гибели четырех ученых из затопленного отсека. Это было единственно верное решение с точки зрения протокола и математики. Но от этого не становится легче. Я отдал приказ, который их убил.
- else: // all_or_nothing
    // --- ВЕТКА ПОТЕРИ СТАНЦИИ (ПРЕЦЕДЕНТ) ---
    Станция «Глубина» потеряна. Все тридцать четыре медведя на борту погибли. В отчете я не стал упоминать, что я поддержал идею рискованной операции Ланы. Это не имеет значения. Решение принимал я. Значит, и ответственность — полностью на мне. Я рискнул, понадеявшись на чудо, и моя надежда убила их всех. Этот провал останется со мной навсегда.
}

Не успел я закончить запись и отослать отчет, как на главной консоли вспыхнул сигнал экстренного вызова от Командования. Новый сигнал бедствия, еще более высокого приоритета. Аванпост «Щит». Нам приказано немедленно следовать туда. Мы — ближайший экипаж. Времени на разбирательства нет.

+ [Принять вызов и проложить курс]
    -> ep6_approach_shield

=== ep6_approach_shield ===
#Location: Podlet_k_Shitu
Мы на подлете к аванпосту «Щит». Ситуация критическая. На экране появляется лицо старого, измотанного медведя. Это ветеран Спастельной службы Тундрин, глава станции.
Тундрин: Капитан, у нас нет времени. Роботы сгорели, нужна ручная активация охлаждения. Я иду сам. Я знаю, что получу дозу, но это мой долг. Жду вашего подтверждения. #П

Игнат: (внезапно вскакивает со своего кресла) Капитан, подождите! Это же реактор модели 7-бис! #У
{ ignat_relationship >= 20:
    // Ветка с высоким доверием: Игнат вспоминает разговор у костра
    Помните, я говорил вам о них? Это тот самый случай! Я знаю, как не просто охладить его, а полностью перезапустить! Я могу сделать так, чтобы реактор продолжил работу, не потребуется никого эвакуировать и станция сможет и дальше функционировать! Я должен пойти! #У
- else:
    // Стандартная ветка
    Я писал по нему дипломную работу, это же совсем новая модель. А Тундрин просто активирует аварийное охлаждение. Это спасет сектор, но реактор превратится в бессмысленный хлам. Я знаю, как его грамотно перезапустить. Я могу спасти станцию! Я должен пойти! #У
}

Лана: Игнат, ты с ума сошел?! Ты понимаешь, какую дозу ты получишь?! Хорошо, если не останешься инвалидом, но ты же и умереть можешь! #З

*   [Оценить варианты] -> ep6_dilemma

= ep6_dilemma
#Location: Captains_bridge
Лана: Капитан, вы не можете этого допустить! Тундрин — доброволец, он знает, на что идет. Устав запрещает рисковать членами спасательной команды, если есть альтернатива! Мы должны позволить ему сделать его работу. #З

Игнат: Капитан, это мой шанс!
{ ignat_relationship >= 20:
    Игнат: Мы можем достичь идеального результата! Да, цена высока. Но я готов ее заплатить. Не останавливайте меня! #Н
- else:
    Игнат: Я могу сделать то, что никто другой не сможет. Да, я заплачу за это здоровьем. Но станция будет жить! Это мой шанс! #Н
}

// --- НОВЫЙ БЛОК: ПРОВЕРКА БОНУСА ОТ ЛАНЫ ---
{ ep2_source_state == "lifeform_saved":
    Лана: (вполголоса) Капитан... есть и третий вариант. Мой полимер с «Источника», я нанесла слой на один из наших рабочих костюмов. Он может защитить Игната. Не полностью, но он снизит дозу до приемлемого уровня. Риск все еще будет, но... это уже не самоубийство. #Н
}

*   [Принять решение] -> ep6_choice

= ep6_choice
#Location: Captains_bridge
Урсан Буреломов: Выбор стал личным. Отправить ветерана Тундрина и потерять реактор (Устав). Или отправить Игната, спасти всё, но с риском покалечить своего товарища и подчиненного (Прецедент).
{ ep2_source_state == "lifeform_saved":
    Или использовать наше возможное преимущество, которое мы получили, спасая ту жизнь на «Источнике».
}

+ [Лана права. Я не могу рисковать своей командой. Ветеран Тундрин, подтверждаю ваш выход.]
    -> ep6_resolve_ustav

+ [Игнат прав. Это его выбор и его звездный час. Игнат, готовьтесь к выходу.]
    -> ep6_resolve_precedent_high_risk

+ {ep2_source_state == "lifeform_saved"} [Игнат, используйте полимер Ланы. Риск приемлем. Готовьтесь.]
    -> ep6_resolve_precedent_low_risk

// --- Ветка Устава ---
= ep6_resolve_ustav
~ ustav_score = ustav_score + 10
~ precedent_score = precedent_score - 5
~ ignat_relationship = ignat_relationship - 5 
~ lana_relationship = lana_relationship + 10 
~ ep6_shield_state = "damage_minimized"
#Location: Captains_bridge
Я запрещаю Игнату покидать корабль. Ветеран Тундрин уходит в реакторный зал. Через десять минут он докладывает об активации охлаждения. Реактор стабилизирован, угроза миновала. Но сам реактор мертв. Станция «Щит» спасена, но бесполезна.
Лана: Вы приняли верное решение, капитан. Вы спасли члена своей команды. #Н
Игнат молча смотрит на потухшую схему реактора. В его глазах — смесь обиды и понимания.
+ [К следующей цели] -> interlude_3

// --- Ветка: Прецедент с ВЫСОКИМ РИСКОМ ---
= ep6_resolve_precedent_high_risk
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship - 10
~ ignat_relationship = ignat_relationship + 15 
~ ep6_shield_state = "station_restored_high_cost"
#Location: Captains_bridge
Я даю добро Игнату. Затаив дыхание, я слежу за его работой. Через полчаса, шатаясь, он возвращается на борт. Медотсек фиксирует критическую дозу облучения. Но станция спасена. Реактор работает.
Игнат: (слабым голосом) Я... я сделал это, капитан. Она работает... Идеально. Спасибо, что поверили в меня. #Р
Лана смотрит на вас с нескрываемым ужасом, оказывая помощь Игнату.
Лана: Вы пожертвовали им ради железки... Вы позволили ему это сделать... Я никогда этого не пойму. #З
+ [К следующей цели] -> interlude_3

// --- Ветка: Прецедент с НИЗКИМ РИСКОМ (бонусная) ---
= ep6_resolve_precedent_low_risk
~ precedent_score = precedent_score + 10
~ ustav_score = ustav_score - 5
~ lana_relationship = lana_relationship + 5
~ ignat_relationship = ignat_relationship + 20
~ ep6_shield_state = "station_restored_ideal"
#Location: Captains_bridge
Я даю добро Игнату, приказав использовать костюм, покрытый полимерным стабилизатором Ланы. Экспериментальным стабилизатором... Я слежу за его работой, затаив дыхание. Он возвращается на борт уставшим, но почти невредимым. Медотсек фиксирует лишь незначительное превышение нормы облучения. Станция спасена. Реактор работает идеально.
Игнат: Получилось! Капитан, ваш расчет и полимер Ланы... все сработало! Это... это триумф! Спасибо, что поверили в меня! #Р
Лана с облегчением выдыхает, проверяя его показатели.
Лана: Это было безумие. Но... вы нашли способ совершить его, не жертвуя никем. Я... впечатлена, капитан. #У
+ [К следующей цели] -> interlude_3

// =================================================================
// ИНТЕРЛЮДИЯ 3: ДРЕЙФ В ТИШИНЕ
// =================================================================

=== interlude_3 ===
#Location: Captains_Quarters
Испытательный срок почти закончен. Мы идем на большой высоте, возвращаясь на базу «Перевал Тысячи Ветров». ... За иллюминатором — темное бархатное небо, где звезды сияют ярче, чем у поверхности. Под нами — одеяло облаков.
Чем ближе к завершению нашего длительного рейда, тем сложнее у членов команды отношения друг с другом. Мы столько пережили!.. 
{
    - ep6_shield_state == "station_restored_high_cost":
        Игнат почти все время проводит в медотсеке, восстанавливаясь после облучения. Лана ухаживает за ним, бросая в мою сторону осуждающие взгляды.
    - ep6_shield_state == "station_restored_ideal":
        Игнат — герой дня. Он постоянно на мостике, с триумфом в глазах дорабатывает отчет о новом протоколе перезапуска реактора. Лана, хоть и ворчит о безрассудстве, с нескрываемым облегчением следит за его показателями.
    - else: // damage_minimized
        Игнат молча сидит у своей инженерной консоли, прогоняя симуляции — очевидно, пытаясь понять, как можно было спасти реактор. Лана выполняет свои обязанности с ледяной отстраненностью.
}

Я один в своей каюте. На терминале — итоговый отчет за прошедший месяц. Время посмотреть, к чему привели мои решения.

*   [Открыть послужной список] -> int3_summary

= int3_summary
#Location: Base_Terminal
На экране — краткая сводка за месяц. Шесть нештатных ситуаций. Шесть решений.

Аванпост «Летний Сад»: {ep1_archive_state == "science_saved": Спасены научные данные.|Сохранено культурное наследие.} <>
Поселение «Тихий Источник»: {ep2_source_state == "harvest_saved": Урожай спасен.|Сохранен уникальный вид.} <>
Станция «Перекресток»: {ep3_crossroads_state == "order_restored": Восстановлен порядок.|Достигнута справедливость.} <>
Проект «Колыбель»: {ep4_cradle_state == "protocol_followed": Спасены жизни ученых.|Исполнена воля ученых.} <>
База «Глубина»: {ep5_depth_state == "majority_saved": Спасено большинство, четверо погибли.|Предпринята фатальная попытка спасти всех.} <>
Аванпост «Щит»: {ep6_shield_state == "damage_minimized": Минимизирован ущерб.|Станция полностью восстановлена.}

Каждая запись — это выбор. Мой выбор. И у каждого была своя цена.
*   [Закрыть отчет] -> int3_dialogue

= int3_dialogue
#Location: Captains_Quarters
Раздается тихий стук в дверь.

{ ustav_score > 25 or precedent_score > 25:
    // Если игрок последовательно придерживался одной из философий,
    // к нему приходит напарник, чью сторону он НЕ выбирал, для финального разговора.
    { ustav_score > precedent_score:
        // Приходит Лана
        -> int3_lana_final_talk
    - else:
        // Приходит Игнат
        -> int3_ignat_final_talk
    }
- else:
    // Если игрок был непоследователен, никто не приходит.
    -> int3_alone
}

= int3_lana_final_talk
На пороге стоит Лана. Она выглядит уставшей.
Лана: Капитан. Я хотела сказать... Я много думала. Я все еще не согласна со многими вашими решениями. Но я видела, как тяжело они вам давались. #П
Лана: Вы действовали так, как считали правильным для системы. Я не могу этого принять, но... я могу это уважать. Вы — капитан, который следует своему компасу. Пусть даже он указывает не туда, куда мой. #Н
+ [Спасибо, Лана. Я ценю это.]
    -> int3_end

= int3_ignat_final_talk
В каюту заходит Игнат. 
{ ep6_shield_state == "station_restored":
    Он все еще выглядит бледным после «Щита», но держится уверенно.
- else:
    Он выглядит отдохнувшим, но чем-то обеспокоенным.
}
Игнат: Капитан. Перед тем, как мы доберемся до базы, я должен сказать. Я часто был не согласен с вами. Вы рисковали там, где устав предписывал осторожность. #Н
Игнат: Но вы брали на себя ответственность за каждый прецедент. Вы не прятались за инструкциями. Вы выносили собственное суждение. Я не одобряю ваш метод, но... я уважаю вашу смелость. #Н
+ [Спасибо, Игнат. Ваше мнение важно для меня.]
    -> int3_end

= int3_alone
#Location: Captains_Quarters
Никто не пришел. Тишина в каюте кажется оглушительной. 
Возможно, пытаясь угодить всем, я в итоге остался один. Мой компас так и не нашел свой север?
Или, может быть, моя команда просто решила, что они все мне сказали раньше?
+ [Остается только ждать.]
    -> int3_end

= int3_end
#Location: Captains_bridge
Мы входим в систему «Перевала Тысячи Ветров». Испытательный срок «Зимородка» завершен. Но мой испытательный срок — еще нет.
На главный экран выводится сообщение от Командования. «Капитан Буреломов, явитесь в зал Совета для финального отчета».
Время нести ответственность.
+ [Войти в зал Совета]
    -> episode_7 // Переход к финалу

// =================================================================
// ЭПИЗОД 7: ФИНАЛ. "ЗАЛ СОВЕТА"
// =================================================================

=== episode_7 ===
#Location: Council_Chamber
Огромный, холодный зал Совета Спасательной службы. Передо мной — трое медведей в парадной форме. В центре — Председатель Совета, старый медведь со шрамом через половину морды. Он молча смотрит на меня, изучая. Тишина давит.

Председатель: Капитан Урсан Буреломов. Ваш испытательный срок окончен. Мы изучили отчеты по всем шести нештатным ситуациям, с которыми вы столкнулись за этот месяц. #Н
Председатель: Вы выбирали между будущим науки и прошлым культуры. Между благополучием поселения и существованием целого вида. #Н
Председатель: Вы решали судьбу станции «Колыбель», выбирая между волей живых и буквой протокола. Вы принимали решение о жизнях на базе «Глубина». Вы приняли тяжелое решение на аванпосте «Щит». #Н
Председатель: Ваш предыдущий послужной список... неоднозначен. Он полон как блестящих успехов, так и вопиющих нарушений. 
{ ustav_perk_unlocked:
    Председатель: Однако нельзя не отметить, что ваши действия вдохновили вашу команду на системные улучшения, которые уже приняты на вооружение всем флотом. #Н
}
{ precedent_perk_unlocked:
    Председатель: Однако нельзя не отметить, что ваше умение находить общий язык обеспечило Спасательной службе новые ремонтные возможности. #Н
}
Председатель: Поэтому мы зададим вам всего один вопрос. После всего, что вы видели за несколько лет службы, чему вы научились? В чем, по-вашему, заключается истинный долг спасателя? #Н

*   [Ответить] -> ep7_choice

= ep7_choice
#Location: Council_Chamber
Это и есть мой финальный отчет. Мой ответ определит все. Мои действия на протяжении этого месяца должны соответствовать тому, что я скажу.

+ [Долг спасателя — в неукоснительном следовании Уставу. Он — система, защищающая от ошибок и спасающая многих. Личные суждения опасны.]
    -> ep7_resolve_ustav

+ [Долг спасателя — в личном суждении. Устав — лишь ориентир. Каждая ситуация уникальна, и только прецедент, основанный на гуманизме, ведет к настоящей помощи.]
    -> ep7_resolve_precedent

+ [Истинный долг — в балансе. Нужно знать Устав, но иметь смелость создать Прецедент. Следовать букве закона, но помнить о его духе.]
    -> ep7_resolve_balance


= ep7_resolve_ustav
#Location: Council_Chamber
{ ustav_score > precedent_score:
    // --- ПОСЛЕДОВАТЕЛЬНЫЙ ПУТЬ УСТАВА ---
    Председатель: Вы говорите об Уставе, и ваши действия это подтверждают. Вы — системный офицер. #Н
    { ignat_relationship >= 20:
        // Идеальная концовка Устава
        Председатель: Вы не просто следовали протоколу, вы смогли убедить в его правоте свою команду. Вы доказали, что система работает. Капитан, мы назначаем вас главой отдела тактического планирования. Нам нужны такие умы. #Р
        -> END
    - else:
        // Стандартная концовка Устава
        Председатель: Вы были эффективны. Вы доказали, что Устав — это надежный инструмент, который спасает жизни. Совет ценит вашу преданность системе. Однако вы так и не смогли сплотить команду вокруг вашей уверенности, не смогли стать для них настоящим лидером, оставшись скорее функцией, чем живым медведем. Вы продолжите службу, и ваш следующий экипаж будет состоять из таких же прагматиков, как и вы. Мы уверены, вы сработаетесь. #Н
        -> END
    }
- else:
    // --- НЕПОСЛЕДОВАТЕЛЬНЫЙ ВЫБОР ---
    Председатель: Вы говорите об Уставе... но ваш послужной список кричит об обратном. Вы нарушали протоколы, когда вам было удобно. Ваши слова расходятся с делом. Спасательный флот не может доверять капитану, чей внутренний компас сломан. Вы отстранены от командования. #З
    -> END
}

= ep7_resolve_precedent
#Location: Council_Chamber
{ precedent_score > ustav_score:
    // --- ПОСЛЕДОВАТЕЛЬНЫЙ ПУТЬ ПРЕЦЕДЕНТА ---
    Председатель: Вы говорите о личном суждении, и ваши действия это подтверждают. Вы — бунтарь, капитан. #Н
    { lana_relationship >= 20:
        // Идеальная концовка Прецедента
        Председатель: Вы нарушали устав, но за вами пошла команда, и вы добились невероятных результатов. Вы доказали, что иногда дух закона важнее его буквы. Мы создаем новую, экспериментальную группу быстрого реагирования. Для самых нештатных ситуаций. Мы хотим, чтобы ее возглавили вы. #Р
        -> END
    - else:
        // Стандартная концовка Прецедента
        Председатель: Ваши успехи неоспоримы. Вы раз за разом доказывали, что личное суждение и смелость могут творить чудеса там, где Устав бессилен. Однако вы так и не смогли убедить в своей правоте всю команду, посеяв в ней сомнения. Вы — превосходный кризисный менеджер, но не тот, кто может вести за собой всех. Поэтому мы переводим вас в резерв. Вы будете привлекаться для самых нештатных, самых безумных операций, где нужен именно ваш талант... и где цена ошибки не так высока. #Н
        -> END
    }
- else:
    // --- НЕПОСЛЕДОВАТЕЛЬНЫЙ ВЫБОР ---
    Председатель: Вы говорите о прецедентах... но при первой же возможности прятались за буквой Устава. Ваши слова расходятся с делом. Спасательный флот не может доверять капитану, чей компас сломан. Вы отстранены от командования. #З
    -> END
}


= ep7_resolve_balance
#Location: Council_Chamber
{ ustav_score == precedent_score:
    // --- ПОСЛЕДОВАТЕЛЬНЫЙ ПУТЬ БАЛАНСА ---
    Председатель: Баланс... Самый сложный из всех путей. #Н
    { ignat_relationship > 0 and lana_relationship > 0:
        // Идеальная "секретная" концовка
        Председатель: Вы не просто выбирали между Уставом и Прецедентом. Вы искали третий путь. И, что самое невероятное, вы смогли сохранить уважение обоих ваших напарников, придерживающихся противоположных взглядов. Вы не просто командир. Вы — дипломат. Капитан, мы предлагаем вам место в комиссии по пересмотру и доработке Устава Спасательной Службы. #У
        -> END
    - else:
        // Плохая концовка Баланса
        Председатель: Вы пытались угодить всем, но в итоге не смогли выработать собственную позицию и потеряли доверие команды. Непоследовательность — худшее качество для капитана. Вы хороший пилот, но не лидер. Вы переводитесь в патрульную службу. #П
        -> END
    }
- else:
    // --- НЕПОСЛЕДОВАТЕЛЬНЫЙ ВЫБОР ---
    Председатель: Вы говорите о балансе... но ваши действия показывают, что вы просто метались из крайности в крайность. Это не мудрость, а расхлябанность. Спасательный флот не может доверять капитану, чей внутренний компас сломан. Вы отстранены от командования. #З
    -> END
}
